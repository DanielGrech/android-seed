buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-android-extensions:0.12.1230'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
//apply plugin: 'crashlytics'
apply plugin: 'spoon'
apply plugin: 'icon-version'

apply from: '../buildscripts/utils.gradle'
apply from: '../buildscripts/jacoco.gradle'

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    signingConfigs {
        release {
            if (config.releaseSigning.storePassword != null) {
                storeFile file(config.releaseSigning.storeFile)
                storePassword config.releaseSigning.storePassword
                keyAlias config.releaseSigning.alias
                keyPassword config.releaseSigning.aliasPassword
            }
        }
    }

    defaultConfig {
        applicationId config.applicationId
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
        buildConfigField "String", "GIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_NUMBER", "\"${getCiBuildNumber()}\""
        buildConfigField "boolean", "CRASHLYTICS_ENABLED", "${isCiBuild()}"
        buildConfigField "boolean", "LEAK_CANARY_ENABLED", "${!isCiBuild()}"

        manifestPlaceholders = [
                // Want QA's to get a crash free build, but devs on their local machine
                // should focus on being as efficient as possible
                "useLargeHeap" : "${isCiBuild()}",
        ]
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-${getCiBuildNumber()}"
            testCoverageEnabled true

            resValue "string", "app_name", "${config.appName} Debug"
        }

        release {
            minifyEnabled false
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_name", "${config.appName}"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'NOTICE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    if (isCiBuild()) {
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def existingFile = output.outputFile

                def variantName = "${variant.flavorName}-${variant.buildType.name.toLowerCase()}"
                def gitBranchName = "${gitBranchName()}"
                def appName = config.appName.toLowerCase().replaceAll(" ", "_")

                def newName = "${appName}-${variantName}-${gitBranchName}-${config.versionCode}-${System.getenv("BUILD_NUMBER")}.apk"
                output.outputFile = new File(existingFile.parent, newName)
            }
        }
    }

    applicationVariants.matching { it.buildType.name != 'release' }.all { createJacocoTask(it) }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":model")
    compile project(":network")
    compile "org.jetbrains.anko:anko:${versions.anko}"
    compile "com.android.support:appcompat-v7:${versions.androidSupport}"
    compile "com.android.support:support-v4:${versions.androidSupport}"
    compile "com.android.support:appcompat-v7:${versions.androidSupport}"
    compile "com.android.support:recyclerview-v7:${versions.androidSupport}"
    compile "com.crashlytics.android:crashlytics:${versions.crashlyticsClient}"
    compile "com.google.dagger:dagger:${versions.dagger}"
    compile "uk.co.chrisjenx:calligraphy:${versions.calligraphy}"
    compile "com.jakewharton.timber:timber:${versions.timber}"
    compile "com.squareup.picasso:picasso:${versions.picasso}"
    compile "javax.annotation:jsr250-api:${versions.jsr250}"
    compile "com.facebook.stetho:stetho-timber:${versions.stetho}"
    compile "com.facebook.device.yearclass:yearclass:${versions.yearClass}"
    compile "com.lacronicus:easydatastorelib:${versions.easyDataStore}"

    compile "io.reactivex:rxandroid:${versions.rxAndroid}"
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:${versions.rxBinding}"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:${versions.rxBinding}"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:${versions.rxBinding}"
    compile "com.trello:rxlifecycle:${versions.rxLifecycle}"

    debugCompile "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}"

    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"

    testCompile "junit:junit:${versions.junit}"
    testCompile "org.robolectric:robolectric:${versions.robolectric}"
    testCompile "org.robolectric:shadows-support-v4:${versions.robolectric}"
    testCompile "org.mockito:mockito-core:${versions.mockito}"
    testCompile "org.assertj:assertj-core:${versions.assertJ}"
}

// Display more verbose information when running tests from CLI
tasks.withType(Test).whenTaskAdded { testTask ->
    testTask.maxParallelForks 4
    testTask.testLogging config.testOptions
}

spoon {
    debug = true
    noAnimations = !isCiBuild()
}