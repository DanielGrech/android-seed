apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.2.201409121644"
}

/**
 * Create a code-coverage report task for the given build variant
 */
def createJacocoTask(variant) {
    def buildTypeName = variant.buildType.name.capitalize()
    def flavorName = variant.flavorName.capitalize()

    def testTaskName = "test${flavorName}${buildTypeName}"

    //noinspection GroovyAssignabilityCheck
    task "${testTaskName}Coverage"(type: JacocoReport, dependsOn: testTaskName) {
        group = "Reporting"
        description = "Generate Jacoco coverage reports for variant ${variant.name}"

        classDirectories = fileTree(
                dir: "${project.buildDir}/intermediates/classes/${variant.dirName}",
                excludes: ['**/R.class',
                           '**/R$*.class',
                           '**/*$$*.*',
                           '**/*_MembersInjector.*',
                           '**/BuildConfig.*',
                           '**/Manifest*.*']
        )

        def coverageSourceDirs = ["${rootDir}/app/src/main/java"]
        additionalSourceDirs = files(coverageSourceDirs)
        sourceDirectories = files(coverageSourceDirs)
        executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

// Export our functions
ext {
    createJacocoTask = this.&createJacocoTask
}